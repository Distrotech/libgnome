AC_INIT(libgnome)

# Making releases:
#   LIBGNOME_MICRO_VERSION += 1;
#   LIBGNOME_INTERFACE_AGE += 1;
#   LIBGNOME_BINARY_AGE += 1;
# if any functions have been added, set LIBGNOME_INTERFACE_AGE to 0.
# if backwards compatibility has been broken,
# set LIBGNOME_BINARY_AGE and LIBGNOME_INTERFACE_AGE to 0.
#
LIBGNOME_MAJOR_VERSION=2
LIBGNOME_MINOR_VERSION=2
LIBGNOME_MICRO_VERSION=0
LIBGNOME_INTERFACE_AGE=0
LIBGNOME_BINARY_AGE=200
# If you need a modifier for the version number. 
# Normally empty, but can be used to make "fixup" releases.
LIBGNOME_EXTRAVERSION=.1

dnl libtool versioning from libgnome

LIBGNOME_CURRENT=`expr 100 '*' $LIBGNOME_MINOR_VERSION + $LIBGNOME_MICRO_VERSION - $LIBGNOME_INTERFACE_AGE`
LIBGNOME_REVISION=$LIBGNOME_INTERFACE_AGE
LIBGNOME_AGE=`expr $LIBGNOME_BINARY_AGE - $LIBGNOME_INTERFACE_AGE`
LIBGNOME_VERSION=$LIBGNOME_MAJOR_VERSION.$LIBGNOME_MINOR_VERSION.$LIBGNOME_MICRO_VERSION$LIBGNOME_EXTRAVERSION

AC_SUBST(LIBGNOME_CURRENT)
AC_SUBST(LIBGNOME_REVISION)
AC_SUBST(LIBGNOME_AGE)

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(libgnome, $LIBGNOME_VERSION)

AM_MAINTAINER_MODE

dnl
dnl Due to the sed scripts being split on 90 line
dnl blocks, this macro needs to be right at the beginning.
dnl
AC_PROG_INTLTOOL

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AM_PROG_LIBTOOL
AC_PROG_YACC
AC_PATH_PROGS(PATH_TO_XRDB, "xrdb")

AC_CHECK_FUNCS([setenv unsetenv clearenv setfsgid])
AC_CHECK_HEADERS(sys/fsuid.h)
AC_CHECK_FUNCS(bind_textdomain_codeset)

GETTEXT_PACKAGE=libgnome-2.0
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE", [The gettext package])


AC_CANONICAL_HOST
  case $host in
  *-linux-gnu | *-irix6*) AC_DEFINE(HAVE_MAN_WHEREIS, 1, [man(1)  understands -w option]);;
  *-solaris*) AC_DEFINE(HAVE_MAN_DEBUG, 1, [man(1) has a -d debug option]);;
  esac

ALL_LINGUAS="am ar az be bg bs ca cs da de el en_GB es et eu fa fi fr gl he hi hu id it ja ko lt lv mk mn ms nl nn no pl pt pt_BR ro ru sk sl sv ta tr uk vi wa zh_CN zh_TW"
AM_GLIB_GNU_GETTEXT
gnomelocaledir='${prefix}/${DATADIRNAME}/locale'
AC_SUBST(gnomelocaledir)

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

GNOME_COMPILE_WARNINGS

dnl Define GNOME_ENABLE_DEBUG if the --enable-debug switch was given.
GNOME_DEBUG_CHECK

dnl Don't use AC_PROG_AWK since we need the full pathname.
AC_PATH_PROGS(AWK, mawk gawk nawk awk, )
AC_PATH_PROGS(PERL, perl5 perl)

# define a MAINT-like variable REBUILD which is set if Perl
# and awk are found, so autogenerated sources can be rebuilt
AC_ARG_ENABLE(rebuilds, [  --disable-rebuilds      disable all source autogeneration rules],,enable_rebuilds=yes)
REBUILD=\#
if test "x$enable_rebuilds" = "xyes" && \
     test -n "$PERL" && \
     $PERL -e 'exit !($] >= 5.002)' > /dev/null 2>&1 && \
     test -n "$AWK" ; then
  REBUILD=
fi
AC_SUBST(REBUILD)

AUDIOFILE_REQUIRED_VERSION=0.2.3
ESOUND_REQUIRED_VERSION=0.2.26
GCONF_REQUIRED_VERSION=1.1.11
GLIB_REQUIRED_VERSION=2.0.3
GNOME_VFS_REQUIRED_VERSION=1.9.16
LIBBONOBO_REQUIRED_VERSION=2.0.0
LIBXML_REQUIRED_VERSION=2.4.22
LIBXSLT_REQUIRED_VERSION=1.0.18

dnl
dnl Start of pkg-config checks
dnl
dnl We first check for esound and audiofile (which has conditional
dnl support built in)
dnl

AUDIOFILE_MODULE="audiofile >= $AUDIOFILE_REQUIRED_VERSION"
ESOUND_MODULE="esound >= $ESOUND_REQUIRED_VERSION"
PKG_CHECK_MODULES(SOUND_TEST, $ESOUND_MODULE $AUDIOFILE_MODULE, [
	AC_DEFINE(HAVE_LIBAUDIOFILE, 1, [Compile with audiofile support])
	AC_DEFINE(HAVE_ESD, 1, [Compile with esd support])
],[
	AUDIOFILE_MODULE=""
	ESOUND_MODULE=""
])

PKG_CHECK_MODULES(LIBGNOME, glib-2.0 >= $GLIB_REQUIRED_VERSION gmodule-2.0 >= $GLIB_REQUIRED_VERSION gnome-vfs-2.0 >= $GNOME_VFS_REQUIRED_VERSION \
libxml-2.0 >= $LIBXML_REQUIRED_VERSION libbonobo-2.0 >= $LIBBONOBO_REQUIRED_VERSION gconf-2.0 >= $GCONF_REQUIRED_VERSION \
$AUDIOFILE_MODULE $ESOUND_MODULE)

PKG_CHECK_MODULES(HELP_VFS_MODULE, glib-2.0 >= $GLIB_REQUIRED_VERSION gmodule-2.0 >= $GLIB_REQUIRED_VERSION gnome-vfs-2.0 >= $GNOME_VFS_REQUIRED_VERSION gnome-vfs-module-2.0 >= $GNOME_VFS_REQUIRED_VERSION \
libxml-2.0 >= $LIBXML_REQUIRED_VERSION libbonobo-2.0 >= $LIBBONOBO_REQUIRED_VERSION)

PKG_CHECK_MODULES(HELP_DOCBOOK_MODULE, glib-2.0 >= $GLIB_REQUIRED_VERSION libxslt >= $LIBXSLT_REQUIRED_VERSION libxml-2.0 >= $LIBXML_REQUIRED_VERSION)
AC_SUBST(HELP_DOCBOOK_MODULE_CFLAGS)
AC_SUBST(HELP_DOCBOOK_MODULE_LIBS)

dnl
dnl Check for gtk-doc
dnl
AC_ARG_WITH(html-dir, [  --with-html-dir=PATH path to installed docs ])

if test "x$with_html_dir" = "x" ; then
  HTML_DIR='${datadir}/gtk-doc/html'
else
  HTML_DIR=$with_html_dir
fi

AC_SUBST(HTML_DIR)

AC_CHECK_PROG(GTKDOC, gtkdoc-mkdb, true, false)

gtk_doc_min_version=0.6
if $GTKDOC ; then 
    gtk_doc_version=`gtkdoc-mkdb --version`
    AC_MSG_CHECKING([gtk-doc version ($gtk_doc_version) >= $gtk_doc_min_version])

    IFS="${IFS= 	}"; gtk_save_IFS="$IFS"; IFS="."
    set $gtk_doc_version
    for min in $gtk_doc_min_version ; do
        cur=$1; shift
        if test -z $min ; then break; fi
        if test -z $cur ; then GTKDOC=false; break; fi
        if test $cur -gt $min ; then break ; fi
        if test $cur -lt $min ; then GTKDOC=false; break ; fi
    done
    IFS="$gtk_save_IFS"

   if $GTKDOC ; then
      AC_MSG_RESULT(yes)
   else
      AC_MSG_RESULT(no)
      GTKDOC=false
   fi
fi

dnl Let people disable the gtk-doc stuff.
AC_ARG_ENABLE(gtk-doc, [  --enable-gtk-doc  Use gtk-doc to build documentation [default=auto]], enable_gtk_doc="$enableval", enable_gtk_doc=auto)

if test x$enable_gtk_doc = xauto ; then
  if test x$GTKDOC = xtrue ; then
    enable_gtk_doc=yes
  else
    enable_gtk_doc=no 
  fi
fi

AM_CONDITIONAL(ENABLE_GTK_DOC, test x$enable_gtk_doc = xyes)

dnl Specify the gconf configuration source, 
dnl default to xml::$(sysconfdir)/gconf/gconf.xml.defaults

dnl ==============================================
dnl Special GConf section 
dnl ==============================================

AC_PATH_PROG(GCONFTOOL, gconftool-2, no)

if test x"$GCONFTOOL" = xno; then
  AC_MSG_ERROR([gconftool-2 executable not found in your path - should be installed with GConf])
fi

AM_GCONF_SOURCE_2

dnl
dnl Substitute these
dnl
AC_SUBST(LIBGNOME_CFLAGS)
AC_SUBST(LIBGNOME_LIBS)

AC_SUBST(HELP_VFS_MODULE_CFLAGS)
AC_SUBST(HELP_VFS_MODULE_LIBS)

GNOME_VFS_VERSION=`$PKG_CONFIG --modversion gnome-vfs-2.0`
AC_SUBST(GNOME_VFS_VERSION)

AC_CHECK_LIB(popt, poptStrippedArgv,, AC_MSG_ERROR([popt 1.5 or newer is required to build
libgnome. You can download the latest version from ftp://ftp.rpm.org/pub/rpm/dist/rpm-4.0.x/]))

dnl ====================================
dnl = zlib for help converters
dnl ====================================
AC_CHECK_LIB(z, gzopen, [Z_LIBS=-lz
AC_SUBST(Z_LIBS)], AC_MSG_ERROR([*** zlib is required]))

dnl ====================================
dnl = Bzip2, for the help converters
dnl ====================================
BZ_LIBS=
AC_CHECK_LIB(bz2, bzread, [AC_DEFINE(HAVE_LIBBZ2, 1, [Compile with libbz2 suppot])
			  BZ_LIBS="-lbz2"])
dnl <= Check for bzip2 > 1.0.0
    AC_CHECK_LIB(bz2, BZ2_bzread, [AC_DEFINE(HAVE_LIBBZ2, 1, [Compile with libbz2 support])
		      	  BZ_LIBS="-lbz2"
    dnl <= define the wrapper-functions to get the Bzip2 stuff working .. =>	
	   AC_DEFINE(bzread,BZ2_bzread, [Needed to get Bzip2 working])
	   AC_DEFINE(bzopen,BZ2_bzopen, [Needed to get Bzip2 working])
	   AC_DEFINE(bzdopen,BZ2_bzdopen, [Needed to get Bzip2 working])
	   AC_DEFINE(bzwrite,BZ2_bzwrite, [Needed to get Bzip2 working])
	   AC_DEFINE(bzerror,BZ2_bzerror, [Needed to get Bzip2 working])
	   AC_DEFINE(bzclose,BZ2_bzclose, [Needed to get Bzip2 working])
	   AC_DEFINE(bzflush,BZ2_bzflush, [Needed to get Bzip2 working])
	   AC_DEFINE(bzCompress,BZ2_bzCompress, [Needed to get Bzip2 working])
	   AC_DEFINE(bzCompressInit,BZ2_bzCompressInit, [Needed to get Bzip2 working])
	   AC_DEFINE(bzDecompress,BZ2_bzDecompress, [Needed to get Bzip2 working])
	   AC_DEFINE(bzDecompressInit,BZ2_bzDecompressInit, [Needed to get Bzip2 working])	
	   ])
    dnl =>
AC_SUBST(BZ_LIBS)

AC_OUTPUT([
Makefile
po/Makefile.in
libgnome/Makefile
monikers/Makefile
schemas/Makefile
gnome-data/Makefile
libgnome/libgnome-2.0.pc
help-converters/Makefile
help-converters/docbook/Makefile
help-converters/docbook/gnome-customization/Makefile
help-converters/man/Makefile
help-converters/info/Makefile
help-converters/gnome-vfs-module/Makefile
doc/Makefile
doc/reference/Makefile
])


